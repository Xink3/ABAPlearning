CLASS lcl_connection DEFINITION.

  PUBLIC SECTION.

    CLASS-METHODS class_constructor.

    METHODS constructor
      IMPORTING
        i_carrier_id    TYPE /dmo/carrier_id
        i_connection_id TYPE /dmo/connection_id
      RAISING
        cx_abap_invalid_value.

    METHODS get_output
      RETURNING
        VALUE(r_output) TYPE string_table.

  PROTECTED SECTION.

  PRIVATE SECTION.

    TYPES:
      BEGIN OF st_details,
        DepartureAirport   TYPE /dmo/airport_from_id,
        DestinationAirport TYPE /dmo/airport_to_id,
        AirlineName        TYPE /dmo/carrier_name,
      END OF st_details.

    TYPES:
      BEGIN OF st_airport,
        AirportId TYPE /dmo/airport_id,
        Name      TYPE /dmo/airport_name,
      END OF st_airport.

    TYPES tt_airports TYPE STANDARD TABLE OF st_airport
                           WITH NON-UNIQUE DEFAULT KEY.

    DATA carrier_id    TYPE /dmo/carrier_id.
    DATA connection_id TYPE /dmo/connection_id.
    DATA details       TYPE st_details.

    CLASS-DATA airports TYPE tt_airports.

ENDCLASS.

CLASS lcl_connection IMPLEMENTATION.

  METHOD class_constructor.

    SELECT FROM /DMO/I_Airport
         FIELDS AirportID, Name
           INTO TABLE @airports.

  ENDMETHOD.

  METHOD constructor.

    IF i_carrier_id IS INITIAL OR i_connection_id IS INITIAL.
      RAISE EXCEPTION TYPE cx_abap_invalid_value.
    ENDIF.

    carrier_id    = i_carrier_id.
    connection_id = i_connection_id.

    SELECT SINGLE
           FROM /DMO/I_Connection
         FIELDS DepartureAirport, DestinationAirport, \_Airline-Name
          WHERE AirlineID    = @carrier_id
            AND ConnectionID = @connection_id
           INTO @details.

  ENDMETHOD.

  METHOD get_output.

    DATA(departure)   = airports[ airportID = details-departureairport   ].
    DATA(destination) = airports[ airportID = details-destinationairport ].

    APPEND |Carrier:     { carrier_id } { details-airlinename }|           TO r_output.
    APPEND |Connection:  { connection_id }|                                TO r_output.
    APPEND |Departure:   { details-departureairport   } { departure-name   }| TO r_output.
    APPEND |Destination: { details-destinationairport } { destination-name }| TO r_output.

  ENDMETHOD.

ENDCLASS.
